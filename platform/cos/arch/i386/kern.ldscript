OUTPUT_FORMAT("elf32-i386", "elf32-i386", "elf32-i386")
OUTPUT_ARCH(i386)
ENTRY(_start)
SECTIONS
{
	. = 1m;
	_begin = . ;
	.text :
	AT (ADDR(.text) & 0x0fffffff)
	{
		*(.multiboot)
		*(.text)
		*(.text.*)
		*(.stub)
		*(.note*)

		. = ALIGN(4);
		PROVIDE (__start_link_set_dkwedge_methods = .);
		*(link_set_dkwedge_methods)
		PROVIDE (__stop_link_set_dkwedge_methods = .);
		. = ALIGN(4);
		PROVIDE (__start_link_set_sysctl_funcs = .);
		*(link_set_sysctl_funcs)
		PROVIDE (__stop_link_set_sysctl_funcs = .);
		. = ALIGN(4);
		PROVIDE (__start_link_set_bufq_strats = .);
		*(link_set_bufq_strats)
		PROVIDE (__stop_link_set_bufq_strats = .);
		. = ALIGN(4);
		PROVIDE (__start_link_set_evcnts = .);
		*(link_set_evcnts)
		PROVIDE (__stop_link_set_evcnts = .);
		. = ALIGN(4);
		PROVIDE (__start_link_set_prop_linkpools = .);
		*(link_set_prop_linkpools)
		PROVIDE (__stop_link_set_prop_linkpools = .);
		. = ALIGN(4);
		PROVIDE (__start_link_set_domains = .);
		*(link_set_domains)
		PROVIDE (__stop_link_set_domains = .);
		. = ALIGN(4);
		PROVIDE (__start_link_set_rump_components = .);
		*(link_set_rump_components)
		PROVIDE (__stop_link_set_rump_components = .);
		. = ALIGN(4);
		PROVIDE (__start_link_set_modules = .);
		*(link_set_modules)
		PROVIDE (__stop_link_set_modules = .);
	}
	_etext = . ;

	.rodata :
	AT (LOADADDR(.text) + (ADDR(.rodata) - ADDR(.text)))
	{
		*(.rodata)
		*(.rodata.*)
	}

	.initfini :
	AT (LOADADDR(.text) + (ADDR(.initfini) - ADDR(.text)))
	{
		__init_array_start = . ;
		*(SORT_BY_INIT_PRIORITY(.init_array.*))
		*(SORT_BY_INIT_PRIORITY(.ctors*))
		*(.init_array)
		__init_array_end = . ;
		__fini_array_start = . ;
		*(SORT_BY_INIT_PRIORITY(.fini_array.*))
		*(SORT_BY_INIT_PRIORITY(.dtors*))
		*(.fini_array)
		__fini_array_end = . ;
	}

	_data_start = .;
	.data :
	AT (LOADADDR(.text) + (ADDR(.data) - ADDR(.text)))
	{
		*(.data)
	}
	_edata = . ;

	.tdata :
	AT (LOADADDR(.text) + (ADDR(.tdata) - ADDR(.text)))
	{
		_tdata_start = . ;
		*(.tdata)
		_tdata_end = . ;
	}

	.tbss :
	AT (LOADADDR(.text) + (ADDR(.tbss) - ADDR(.text)))
	{
		_tbss_start = . ;
		*(.tbss)
		_tbss_end = . ;
	}

	__bss_start = . ;
	.bss :
	AT (LOADADDR(.text) + (ADDR(.bss) - ADDR(.text)))
	{
		*(.bss)
		*(.bss.*)
		*(COMMON)
		*(.bootstack)
	}
	_end = . ;
	PROVIDE (end = .) ;
}
